import org.apache.tools.ant.filters.ReplaceTokens
import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
    id 'com.diffplug.spotless' version '5.8.2'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

description = 'Check if you current chunk is a slime one!'
group = 'dev.bloodstone'
version = '1.0.0'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenLocal()
    maven {
        name = 'papermc'
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}
def cloudVersion = '1.5.0'
dependencies {
    compileOnly group: 'com.destroystokyo.paper', name: 'paper-api', version: '1.16.5-R0.1-SNAPSHOT'
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation group: 'cloud.commandframework', name: 'cloud-paper', version: cloudVersion
    implementation group: 'cloud.commandframework', name: 'cloud-annotations', version: cloudVersion
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "${project.property('group')}.${project.property('name').toString().toLowerCase()}.lib"
}

shadowJar {
    dependsOn tasks.relocateShadowJar
    minimize()
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [
                version: version,
                description: description,
        ]
    }
}

compileJava {
    options.encoding = 'UTF-8' // Encode your source code with UTF-8
    options.compilerArgs += ["-parameters"]
}


compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        javaParameters = true
    }
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

spotless {
    format 'misc', {
        target '**/*.gradle', '**/*.md'
        trimTrailingWhitespace()
        indentWithSpaces(4)
    }
    kotlin {
        ktlint('0.39.0')
        licenseHeader """
/*
 *  ${name} - ${description}
 *  Copyright (C) 2020  Prof_Bloodstone
 *
 *  This program is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU Affero General Public License as published
 *  by the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  This program is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU Affero General Public License for more details.
 *
 *  You should have received a copy of the GNU Affero General Public License
 *  along with this program.  If not, see <https://www.gnu.org/licenses/>.
*/
"""
    }
}
