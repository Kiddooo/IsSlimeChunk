import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
    id 'com.diffplug.spotless' version '5.8.2'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

description = 'Check if you current chunk is a slime one!'
group = 'dev.bloodstone'
version = '1.0.4'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenLocal()
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}
dependencies {
    compileOnly group: 'io.papermc.paper', name: 'paper-api', version: '1.21-R0.1-SNAPSHOT'
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation group: 'org.incendo', name: 'cloud-paper', version: '2.0.0-beta.9'
    implementation group: 'org.incendo', name: 'cloud-annotations', version: '2.0.0-rc.2'
}

tasks.register('relocateShadowJar', ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "${project.property('group')}.${project.property('name').toString().toLowerCase()}.lib"
}

shadowJar {
    dependsOn tasks.relocateShadowJar
    minimize()
}

processResources {
    def props = [
        version: version,
        description: project.description,
    ]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

compileJava {
    options.encoding = 'UTF-8' // Encode your source code with UTF-8
    options.compilerArgs += ["-parameters"]
}


compileKotlin {
    kotlinOptions {
        jvmTarget = "17"
        javaParameters = true
    }
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "17"
}

spotless {
    format 'misc', {
        target '**/*.gradle', '**/*.md'
        trimTrailingWhitespace()
        indentWithSpaces(4)
    }
    kotlin {
        ktlint('0.39.0')
        licenseHeader """
/*
 *  ${name} - ${description}
 *  Copyright (C) 2020  Prof_Bloodstone
 *
 *  This program is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU Affero General Public License as published
 *  by the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  This program is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU Affero General Public License for more details.
 *
 *  You should have received a copy of the GNU Affero General Public License
 *  along with this program.  If not, see <https://www.gnu.org/licenses/>.
*/
"""
    }
}
